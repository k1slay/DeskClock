plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk deps.app.compile

    defaultConfig {
        applicationId "com.k2.deskclock"
        minSdk deps.app.min
        targetSdk deps.app.target
        versionCode 3
        versionName "Beta 1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            debuggable = false
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            debuggable = true
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.4"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    namespace 'com.k2.deskclock'

    signingConfigs {
        debug {
            storeFile file("keystores/debug.keystore")
            storePassword "testPass123"
            keyAlias "testAlias"
            keyPassword "testPass123"
        }
    }

}

dependencies {
    implementation project(path: ':weather')
    implementation project(path: ':wallpaper')
    implementation project(path: ':localstore')
    implementation project(path: ':commons')
    implementation project(path: ':location')

    implementation platform("com.google.firebase:firebase-bom:$deps.versions.firebaseBom")
    deps.firebase.values().forEach { dep ->
        implementation dep
    }

    deps.flipper.values().forEach { dep ->
        debugImplementation dep
    }

    implementation platform("androidx.compose:compose-bom:$deps.versions.composeBom")
    deps.compose.values().forEach { composeDep ->
        implementation composeDep
    }
    deps.coil.values().forEach { dep ->
        implementation dep
    }

    // hilt
    implementation deps.hilt.base
    kapt deps.hilt.processor

    implementation "com.squareup.retrofit2:retrofit:$deps.versions.retrofit"

    deps.instrumentedTest.values().forEach { dep ->
        androidTestImplementation dep
    }
    deps.unitTest.values().forEach { dep ->
        testImplementation dep
    }

}
